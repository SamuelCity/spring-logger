import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'
    id 'maven-publish'
    id "io.freefair.lombok" version '6.6.1'
    id 'org.springframework.boot' version '3.1.0' apply false
    id 'io.spring.dependency-management' version '1.1.0'
}

allprojects {
    apply plugin: 'io.spring.dependency-management'

    group = 'com.gocity'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    publishToMavenLocal {
        dependsOn build
    }
}

configure(subprojects) { subproject ->
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.freefair.lombok'

    sourceCompatibility = '17'

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }

                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
